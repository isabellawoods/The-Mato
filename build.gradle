buildscript {
    repositories {
        mavenCentral()
        maven {
            url = 'https://maven.minecraftforge.net'
        }
        maven {
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = minecraft_version + "-v" + mod_version
// http://maven.apache.org/guides/mini/guide-naming-conventions.html
group = mod_group_id
archivesBaseName = mod_name_no_spaces

// Mojang ships Java 8 to end users, so your mod should target Java 8.
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    // The mappings can be changed at any time, and must be in the following format:
    // Channel:     Version:
    // snapshot   YYYYMMDD    Snapshot are built nightly;
    // stable         #                       Stables are built at the discretion of the MCP team;
    // official        MCVersion      Official field/method names from Mojang mapping files.

    // You must be aware of the Mojang license when using the 'official' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md

    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: mappings_channel, version: minecraft_version
    // A SRG named sources jar is made by default. Uncomment this to disable.
    // makeObfSourceJar = false

    // This property enables access transformers for use in development, which will be applied to the Minecraft artifact.
    // The access transformer file can be anywhere in the project. However, it must be at "META-INF/accesstransformer.cfg" in the final mod jar to be loaded by Forge.
    // This default location is a best practice to automatically put the file in the right place in the final jar.
    // See https://docs.minecraftforge.net/en/latest/advanced/accesstransformers/ for more information.
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        // Applies to all run configurations below.
        configureEach {
            workingDirectory project.file('run')

            // Recommended logging data for a user development environment.
            // The markers can be added/removed as needed, and are separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console.
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            // Disables the ref map for mixins.
            property 'mixin.env.disableRefMap', 'true'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            // Copied from Tinker's Construct repository.
            if (project.hasProperty('mc_uuid')) {
                // Your uuid without any dashes in the middle.
                args '--uuid', project.getProperty('mc_uuid')
            }
            if (project.hasProperty('mc_username')) {
                // Your username/display name, this is the name that shows up in chat.
                // Note: This is not your email, even if you have a Mojang account.
                args '--username', project.getProperty('mc_username')
            }

            args '--version', 'melony-studios-dev/themato'

            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property "mixin.debug.export", "true"
        }

        server {
            args '--nogui'
        }

        data {
            // Specify the mod id for data generation, where to output the resulting resources, and where to look for existing resources.
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, mod_id + ".refmap.json"
}

// Include resources generated by data generators.
sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    // Specify the Minecraft version to use.
    // If this is any group other than 'net.minecraft', it is assumed that the dep is a Forge Gradle 'patcher' dependency, and its patches will be applied.
    // The user development artifact is a special name and will get all sorts of transformations applied to it.
    // If the group id is "net.minecraft" and the artifact id is "client", "server" or "joined", then special handling is done to allow a setup of a vanilla dependency without the use of an external repository.
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Compile against the Just Enough Items API but do not include it at runtime.
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
    // At runtime, use the full Just Enough Items jar.
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")

    // Annotation Processors
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    // You may put jars on which you depend on in ./libs, or you may define them like so:
    // compile "some.group:artifact:version:classifier"
    // compile "some.group:artifact:version"

    // Real examples:
    // compile 'com.mod-buildcraft:buildcraft:6.0.8:dev' // Adds Buildcraft to the development environment.
    // compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // Adds EJML (Efficient Java Matrix Library) to the development environment.

    // The 'provided' configuration is for optional dependencies that exist at compile-time but might not at runtime.
    // provided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // These dependencies get remapped to your current MCP mappings:
    // deobf 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // For more information, see:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
}

repositories {
        maven {
        // Just Enough Items
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        // Location of a maven mirror for Just Enough Items files, as a fallback.
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        url "https://www.cursemaven.com"
    }
}

// Example for how to get properties into the manifest for reading by the runtime:
jar {
    manifest {
        attributes([
            "Specification-Title": mod_name,
            "Specification-Vendor": mod_authors,
            // We are version 1 of ourselves.
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor": mod_authors,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": mod_id + ".mixins.json"
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task.
// This is the preferred method to reobfuscate your jar file.
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, development time needs unobfuscated jar files, so you can delay the obfuscation until publishing by doing
// publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}